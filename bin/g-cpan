#!/usr/bin/perl -w
package main;

# Copyright 1999-2006 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
#

# modules to use - these will need to be marked as
# dependencies, and installable by portage
use warnings;
use strict;
use File::Spec;
use File::Path;
use File::Basename;
use File::Copy;
use Term::ANSIColor;
use Cwd qw(getcwd abs_path cwd);
use YAML;
use YAML::Node;
use Carp;
use IO::File;
use DirHandle;

# Module load & configure
use Getopt::Long;
Getopt::Long::Configure('bundling');
use Log::Agent;
use Log::Agent::Driver::File;
use Log::Agent::Driver::Silent;

our $VERSION = '0.16_rc0';
my $prog = basename($0);

##### Establish our tmpdir
if (! $ENV{TMPDIR}) { $ENV{TMPDIR} = '/var/tmp/g-cpan' }

my %dep_list = ();
my @perl_dirs = qw("perl-gcpan" "dev-perl" "perl-core" "perl-text" "perl-tools" "perl-xml" "perl-dev" "dev-lang");

###############################
# Command line interpretation #
###############################

# Declare options
# First, the main switches
my @main_switches = \my ($search, $list, $install, $generate, $buildpkgonly);

# Then, additional switches
my @additional_switches = \my ($upgrade, $pretend, $buildpkg, $ask);

# Then, the normal options
my ($debug, $verbose, $cpan_reload, $log);

# Set colors here so we can use them at will anywhere :)

use Gentoo;
use Gentoo::UI::Console;

#Get & Parse them
GetOptions(
    'verbose|v'      => \$verbose,
    'search|s'       => \$search,
    'install|i'      => \$install,
    'upgrade|u'      => \$upgrade,
    'list|l'         => \$list,
    'log|L'          => \$log,
    'pretend|p'      => \$pretend,
    'buildpkg|b'     => \$buildpkg,
    'buildpkgonly|B' => \$buildpkgonly,
    'ask|a'          => \$ask,
    'generate|g'     => \$generate,
    'debug|d'        => \$debug,
    'cpan_reload'    => \$cpan_reload,
    'help|h'         => sub { exit_usage(); }
) or exit_usage();


if ($log)
{
    open my $log_test, q{>>}, "/var/log/$prog/$prog.err"
      or fatal(print_err("You don't have permission to perform logging to /var/log/$prog/$prog.err: $!"));
    close $log_test or fatal(print_err("Unable to close $!"));

    my $log_driver = Log::Agent::Driver::File->make(
        -prefix     => "$prog",
        -magic_open => 0,
        -stampfmt   => 'date',
        -channels   => {
            'error'  => "/var/log/$prog/$prog.err",
            'output' => "/var/log/$prog/$prog.log",
        }
    );
    logconfig(-driver => $log_driver);
}
else
{
    my $log_driver = Log::Agent::Driver::Silent->make();
    logconfig(-driver => $log_driver);
}

if ($log && $debug) { print_warn('*WARNING* - logging debug output can create large logs') }

if (($install || $ask || $buildpkg || $buildpkgonly || $upgrade) && $> > 0 && !$pretend)
{
    print_err('INSUFFICIENT PERMISSIONS TO RUN EMERGE');
    logerr('ERROR - INSUFFICIENT PERMISSIONS TO RUN EMERGE');
    exit;
}

if (!$install && defined $ask)
{
    $install = 1;
}

# Array containing the original values passed for installing
my %passed_to_install;

@ARGV > 0
  and %passed_to_install = map { $_ => 1 } @ARGV;

# Array that will contain the portage friendly version of the values passed to install
my %really_install;

# Output error if more than one main switch is activated
#

if ((grep { defined $$_ } @main_switches) > 1)
{
    print_err('You can\'t combine actions with each other.');
    print_out("${white}Please consult ${cyan}$prog ${green}--help${reset} or ${cyan}man $prog${reset} for more information\n\n");
    exit;
}

if (!grep { defined $$_ } @main_switches, @additional_switches)
{
    print_err("You haven\'t told $prog what to do.\n");
    print_out("${white}Please consult ${cyan}$prog ${green}--help${reset} or ${cyan}man $prog${reset} for more information\n\n");
    exit;
}

# Output error if no arguments
if (@ARGV == 0 && !(defined $upgrade || defined $list))
{
    print_err('No module name or expression given!');
    print_out("${white}Please consult ${cyan}$prog ${green}--help${reset} for more information\n\n");
    exit;
}

######################
# CPAN Special Stuff #
######################

my $GentooCPAN = Gentoo->new();

# Don't do autointalls via ExtUtils::AutoInstall
$ENV{PERL_EXTUTILS_AUTOINSTALL} = "--skipdeps";
$ENV{PERL_MM_USE_DEFAULT}       = 1;

# Do we need to generate a config ?
eval 'use CPAN::Config;';
my $needs_cpan_stub = $@ ? 1 : 0;

# Test Replacement - ((A&B)or(C&B)) should be the same as ((A or C) and B)
if (($needs_cpan_stub || $> > 0) && !-f "$ENV{HOME}/.cpan/CPAN/MyConfig.pm")
{

    # In case match comes from the UID test
    $needs_cpan_stub = 1;

    print_warn('No CPAN Config found, auto-generating a basic one');

    # Generate a fake config for CPAN
    $GentooCPAN->makeCPANstub();
}
else
{
    $needs_cpan_stub = 0;
}

use CPAN;

{
    foreach (qw[build sources])
    {
        if ((-d "$ENV{TMPDIR}/.cpan/$_") && (! -w "$ENV{TMPDIR}/.cpan/$_"))
        {
                print_err("No WRITE permissions in $ENV{TMPDIR}/.cpan/$_!!");
                print_err("Please run $prog as a user with sufficient permissions");
                print_err("or correct permissions in $ENV{TMPDIR}");
                exit;
        }
    }
}
##########
# main() #
##########

# Confirm that there is an /etc/portage/categories file
# and that we have an entry for perl-gcpan in it.
my $cat_file = '/etc/portage/categories';
if (-f "$cat_file")
{

    #
    #  Use braces to localize the $/ assignment, so we don't get bitten later.
    #
    local $/ = undef;
    my $cat_read = IO::File->new($cat_file, '<');
    if (defined $cat_read)
    {
        my $data = <$cat_read>;
        undef $cat_read;
        autoflush STDOUT 1;
        if ($data !~ m{gcpan}gmxi)
        {
            my $cat_write = IO::File->new($cat_file, '>>');
            if (defined $cat_write)
            {
                print {$cat_write} "perl-gcpan\n";
                undef $cat_write;
                autoflush STDOUT 1;
            }
            else
            {
                print_err('Insufficient permissions to edit /etc/portage/categories');
                print_err("Please run $prog as a user with sufficient permissions");
                exit;
            }
        }
    }
}
else
{
    my $cat_write = IO::File->new($cat_file, '>');
    if (defined $cat_write)
    {
        print {$cat_write} 'perl-gcpan';
    }
    else
    {
        print_err('Insufficient permissions to edit /etc/portage/categories');
        print_err("Please run $prog as a user with sufficient permissions");
        exit;
    }
}

my $gcpan_run = Gentoo->new(
    'cpan_reload' => $cpan_reload,
    'DEBUG'       => $debug,
);

# Get the main portdir
my $PORTAGE_DIR = $gcpan_run->getEnv('PORTDIR');
$gcpan_run->{portage_bases}{$PORTAGE_DIR} = 1;

# Grab the keywords - we'll need these when we build the ebuild
my $keywords = $gcpan_run->getEnv('ACCEPT_KEYWORDS');
if ($keywords =~ m{ACCEPT_KEYWORDS}xm) { $keywords=q{} }
$keywords ||= do
{
    open my $tmp, '<', "$PORTAGE_DIR/profiles/arch.list"
      or fatal(print_err("ERROR READING $PORTAGE_DIR/profiles/arch.list: $!"));
    join q{ }, map { chomp; $_ } <$tmp>;
};

$ENV{ACCEPT_KEYWORDS} = $keywords;
# Get the overlays
my $overlay = $gcpan_run->getEnv('PORTDIR_OVERLAY') || undef;
# Get the DISTDIR - we'd like store the tarballs in here the one time
$gcpan_run->{sources} = ($gcpan_run->getEnv('DISTDIR'));

# Make sure we have write access to the DISTDIR
if (   $generate
    || $install
    || $pretend
    || $buildpkg
    || $buildpkgonly
    || $ask
    || $upgrade)
{
	if (( -d $gcpan_run->{sources}) && (! -w $gcpan_run->{sources}))
    {
        fatal(print_err("No write access to DISTDIR: $!"));
    }
}

if ($overlay)
{
    if ($overlay =~ m{\S*\s+\S*}xm)
    {
        my @overlays = split q{ }, $overlay;
        foreach (@overlays)
        {
           if ( -d $_ ) { $gcpan_run->{portage_bases}{$_} = 1 }
        }
    }
    else
    {
        if (-d $overlay) { $gcpan_run->{portage_bases}{$overlay} = 1 }
    }
    if (keys %{$gcpan_run->{portage_bases}} < 1)
    {
        fatal(print_err('DEFINED OVERLAYS DON\'T EXIST!'));
    }

}
elsif ($generate || $pretend)
{
    print_err("The option you have chosen isn't supported without a configured overlay.\n");
    exit;
}

# Set portage_categories to our defined list of perl_dirs
$gcpan_run->{portage_categories} = \@perl_dirs;

# Taking care of Searches.
if ($search)
{
    foreach my $expr (@ARGV)
    {
        my $tree_expr = $expr;
        $tree_expr =~ s/::/-/gxms;
        scanTree(lc($tree_expr));
        if (defined $gcpan_run->{portage}{lc($tree_expr)}{found})
        {
            print_info("$gcpan_run->{portage}{lc($tree_expr)}{category}/$gcpan_run->{portage}{lc($tree_expr)}{name}");
            my $tdesc = strip_ends($gcpan_run->{portage}{lc($tree_expr)}{DESCRIPTION});
            my $thp   = strip_ends($gcpan_run->{portage}{lc($tree_expr)}{HOMEPAGE});
            print_info("DESCRIPTION: $tdesc");
            print_info("HOMEPAGE: $thp");
        }
        else
        {
            print_info("No ebuild exists, pulling up CPAN listings for $expr");
            # Assume they gave us module-name instead of module::name
            # which is bad, because CPAN can't convert it ;p

            $verbose and print_info("Searching for $expr on CPAN");

            spinner_start();
				CPAN::Shell->i("/$expr/");
            # remove the spin
            spinner_stop();
        }
    }

    exit;
}

if ($list || $upgrade)
{
    if ($upgrade && @ARGV > 0)
    {
        %passed_to_install = map { $_ => 1 } @ARGV;
    }
    else
    {
        my @overlays = split q{ }, $overlay;
        foreach my $overlay_dir (@overlays)
        {
            my $gcpan_dir = File::Spec->catdir($overlay_dir, 'perl-gcpan');

            #$list and print_info("OVERLAY: $gcpan_dir");
            print_info("OVERLAY: $gcpan_dir");
            if (opendir PDIR, $gcpan_dir)
            {
                while (my $file = readdir PDIR)
                {
                    next if ($file eq q{.} || $file eq q{..});
                    $list and print_info("perl-gcpan/$file");
                    $upgrade and $passed_to_install{$file} = 1;
                }
                closedir PDIR;
            }
            else
            {
                print_warn("Couldn't open folder $gcpan_dir: $!");
            }
        }
    }
}

if (   $generate
    || $install
    || $pretend
    || $buildpkg
    || $buildpkgonly
    || $ask
    || $upgrade)
{
	if (keys (%passed_to_install)) {
		generatePackageInfo($_) foreach (keys %passed_to_install);
	}
}

if (($install || $pretend || $buildpkg || $buildpkgonly || $upgrade || $ask)
	&& !( $generate))
{
    if (keys %really_install)
    {

        my @ebuilds = (keys %really_install);
        $verbose and print_info("Calling emerge for @ebuilds\n");
        my @flags;
        if ($pretend      && $pretend > 0)      { push @flags, '--pretend' }
        if ($ask          && $ask > 0)          { push @flags, '--ask' }
        if ($buildpkg     && $buildpkg > 0)     { push @flags, '--buildpkg' }
        if ($buildpkgonly && $buildpkgonly > 0) { push @flags, '--buildpkgonly' }
		if ($upgrade && $upgrade > 0) { push @flags, '--update' }

        $verbose and print_info("Calling: emerge @flags @ebuilds");
        $gcpan_run->emerge_ebuild(@ebuilds, @flags);

    }
    else
    {
        if ($upgrade)
        {
            print_info('Everything was up to date, nothing to do!');
        }
        else
        {
            print_err('Nothing to install!!');
        }
    }
}




sub strip_ends
{
    my $key = shift;
    if (defined $ENV{$key})
    {
        $ENV{$key} =~ s{\\n}{ }gxms;
        $ENV{$key} =~ s{\\|\'|\\'|\$|\s*$}{}gmxs;

        return $ENV{$key};
    }
    else
    {
        $key =~ s{\\n}{ }gxms;

        $key =~ s{(\'|\\|\\'|\$|\s*$)}{}gmxs;

        return $key;
    }
}

sub exit_usage
{
	use Pod::Usage;
	pod2usage(-verbose => 2);
	exit;
}

# cab - Takes care of system's sanity
END
{

    #Clean out the /tmp tree we were using
    #I know this looks weird, but since clean_up is invoked on a search, where OVERLAYS isn't ever defined,
    # we first need to see if it exists, then need to remove only if it has content (the old exists vs. defined)

    if (defined $ENV{TMPDIR})
    {
        $verbose and print_ok('Cleaning temporary space');
        my ($startdir) = cwd();
        chdir("$ENV{TMPDIR}/.cpan");
        opendir(CURD, q{.});
        my @dirs = readdir(CURD);
        closedir(CURD);
        foreach my $dir (@dirs)
        {
            $dir eq q{.}       and next;
            $dir eq q{..}      and next;
            $dir eq q{sources} and next;
            -d $dir and -w $dir and rmtree([qq{$ENV{TMPDIR}/.cpan/$dir}]);
        }
        if (-d "$ENV{TMPDIR}/perl-gcpan" and -w "$ENV{TMPDIR}/perl-gcpan") { rmtree([qq{$ENV{TMPDIR}/perl-gcpan}]) }
    }
}

exit;

##############

__END__

=pod  

=head1 NAME

g-cpan - install CPAN-provided Perl modules using Gentoo's Portage 

=head1 SYNOPSIS

B<g-cpan> [B<-a>| B<--ask>]  [B<-g>| B<--generate>]  [B<-i>| B<--install>]  [B<-l>| B<--list >]  [B<-p>| B<--pretend >] [B<-s>| B<--search>] [B<-u>| B<--upgrade >] [B<-v>| B<--verbose>]  <I<module-name>> 

=head1 DESCRIPTION

B<g-cpan> is a perl script that installs a CPAN module (including its dependencies) using Gentoo's Portage.  It saves dependency information and stored files as if they were installed through a regular ebuild.  

=head1 CPAN Configuration

B<~/.cpan/CPAN/MyConfig.pm>
B</path/to/perl/CPAN/Config.pm> 

When you run B<g-cpan>, it will check for two configuration files. If you are root, it will check for the presense of an already configured CPAN under your perl install path. If CPAN is not configured, or you are not root, g-cpan will create a generic configuration for CPAN in ~/.cpan/CPAN/ called MyConfig.pm. You can modify this file as needed at any time. 

The CPAN configuration file is used for interacting with CPAN, determining what modules are available, what modules are needed, and performing all basic CPAN functions. 

=head1 g-cpan and Overlays

B<g-cpan> is now overlay "friendly." B<g-cpan> will scan both the overlays provided in your make.conf as well as any you have set via environment variables, to help determine its course of action. If you have  defined overlays, B<g-cpan> will use the first overlay in your list that the user running it can write to. Any ebuilds generated by B<g-cpan> will be stored in this overlay for future use (such as upgrading). 

If no overlays are defined, or the user operating B<g-cpan> cannot write to an overlay, then anything generated will be written to a temporary space and wiped on exit. 

=head1 Arguments

The following arguments are accepted by g-cpan.

=over 4

=item B<-g,--generate>

Generate ebuilds and drop them in the overlay, but never call portage. Useful for generating a tree of ebuilds without having permissions to portage. 

=item B<-i,--install>

Install the (list of) modules provided. 

=item B<-l,--list>

List ebuilds that have been generated by g-cpan and reside in your overlay. 

=item B<-L,--log>

Log all output to /var/log/g-cpan/g-cpan.log and g-cpan.err

=item B<-s,--search>

Search CPAN for the provided module. 

=item B<--cpan_reload>

Reload CPAN's index online

=item B<-u,--upgrade>

Attempt to upgrade any ebuilds generated by g-cpan to newer versions. This option will create new ebuilds for those that exist in your overlay already. If a module name is given, it will attempt to only upgrade the requested module. If no arguments are given, all modules in your g-cpan overlay will be checked. 

=item B<-v,--verbose>

Enable verbose mode for more feedback on the step by step processes that g-cpan is running 

=back

=head1 Portage Arguments

The following portage arguments can be passed to g-cpan. Please read the portage documentation for more information on how they work.

=over 4

=item B<-a,--ask>

Pass the "ask" argument to portage. This will cause portage to prompt you to confirm your installation prior to issuing an emerge 

=item B<-b,--buildpkg>

Tells  emerge to build binary packages for all ebuilds processed in addition to actually merging the packages.

=item B<-B,--buildpkgonly>

Creates  binary packages for all ebuilds processed without actually merging the packages.

=item B<-p,--pretend>

Create ebuilds as needed for the (list of) modules provided, but don't perform the actual emerge. 


=back

=head1 Usage


=head2 Here we install Jabber::RPC

# g-cpan -i Jabber::RPC 

=head2 Performing a search

# g-cpan -s Clone::Any 

=head2 Upgrading overlay from g-cpan

# g-cpan -u 

=head2 Upgrading specifc ebuild in overlay from g-cpan

# g-cpan -u Mickey-Mouse 

=head1 Todo

=head1 See Also

L<B<emerge>(1)>, L<B<ebuild>(5)>, L<B<make.conf>(5)>, L<B<perl>(1)> 

=head1 Authors

mcummings <mcummings@gentoo.org> cab <cab@gentoo.org> sniper <sniper@mongueurs.net> beu <beu@gentoo.org> J Robert Ray <jrray@gentoo.org> (g-cpan) RAC <rac@gentoo.org>  Sven Vermeulen <swift@gentoo.org> (Original manpage) Damien Krotkine <dams@gentoo.org> Yuval Yaari <yuval@gentoo.org>

=head1 Reporting Bugs 

Please report bugs via http://bugs.gentoo.org/ or https://bugs.gentoo.org/ 

=head1 svn Id $Id$ 

=cut

